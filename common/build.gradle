plugins {
    id 'java-library'
}

repositories {
    maven {
        name = "Mojang Libraries"
        url = "https://libraries.minecraft.net"
        content {
            includeGroup "com.mojang"
        }
    }
}

configurations {
    include


    includeApi

    include.extendsFrom includeApi
    api.extendsFrom includeApi
}

dependencies {
    compileOnly "com.mojang:brigadier:1.3.10"
    includeApi "top.offsetmonkey538.offsetconfig538:offsetconfig538:${rootProject.offsetconfig538_version}"
}

processResources {
    final Map properties = Map.of(
            "modVersion",                 project.version
    )
    inputs.properties(properties)

    filesMatching(["fabric.mod.json", "META-INF/neoforge.mods.toml"]) {
        expand(properties)
    }
}

jar {
    // Include dependencies
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.include.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
