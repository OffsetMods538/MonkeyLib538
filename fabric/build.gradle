import dex.plugins.outlet.v2.util.ReleaseType

plugins {
	id 'multiloader-modded'
	id 'fabric-loom' version '1.14-SNAPSHOT'
	id 'io.github.dexman545.outlet' version '1.6.1'
	id 'com.modrinth.minotaur' version '2.+' apply false
}

loom {
	afterEvaluate {
		runClient.setEnabled(false)
		runServer.setEnabled(false)
	}
}

dependencies {
	compileOnlyApi project(":modded")
	commonJava project(path: ":modded", configuration: "commonJava")
	commonResources project(path: ":modded", configuration: "commonResources")
    //TODO: shadow? guess if modded includes with shadow, this one doesn't? include "top.offsetmonkey538.offsetconfig538:offsetconfig538:${rootProject.offsetconfig538_version}"
    //TODO: shadow? guess if modded includes with shadow, this one doesn't? include "blue.endless:jankson:${rootProject.jankson_version}"
}

allprojects {
	apply plugin: "multiloader-modded"
	apply plugin: "fabric-loom"
	apply plugin: "io.github.dexman545.outlet"

	outlet {
		maintainPropertiesFile = System.getenv("DISABLE_PROPERTIES_UPDATE") == null
		mcVersionRange = project.supported_minecraft_versions
		allowedReleaseTypes = Set.of(ReleaseType.RELEASE)
		propertiesData = [
				'loader_version': outlet.loaderVersion(),
				'fapi_version'  : outlet.fapiVersion(project.minecraft_version)
		]
	}

	dependencies {
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_version}:v2" // loom.officialMojangMappings()
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fapi_version}"
	}
}

subprojects {
	apply plugin: "io.github.dexman545.outlet"
	apply plugin: "com.modrinth.minotaur"

	tasks.modrinth.dependsOn(tasks.modrinthSyncBody)

	repositories {
		maven {
			name = "DevAuth"
			url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
			content {
				includeGroup "me.djtheredstoner"
			}
		}
	}

	dependencies {
		compileOnlyApi project(":fabric")
		commonJava project(path: ":fabric", configuration: "commonJava")
		commonResources project(path: ":fabric", configuration: "commonResources")

		compileOnlyApi project(":modded:${project.commonModdedVersion}")
		commonJava project(path: ":modded:${project.commonModdedVersion}", configuration: "commonJava")
		commonResources project(path: ":modded:${project.commonModdedVersion}", configuration: "commonResources")

		// DevAuth
		modLocalRuntime "me.djtheredstoner:DevAuth-fabric:${rootProject.devauth_version}"
	}

	loom {
		runs {
			server {
				name = "${project.project_name} Server"
				runDir "run/server"
				ideConfigGenerated(true)
			}
			client {
				name = "${project.project_name} Client"
				runDir "run/client"
				vmArg "-Ddevauth.enabled=true"
				ideConfigGenerated(true)
			}
		}
	}

	modrinth {
		// Main properties
		token = System.getenv("MODRINTH_TOKEN")
		projectId = "monkeylib538"
		gameVersions = outlet.mcVersions()
		loaders = ["fabric"]


		// Version stuff
		def customVersionName = System.getenv("VERSION_NAME")
		if (customVersionName != null) versionName = customVersionName

		versionNumber = "${project.version}"

		def isPreRelease = System.getenv("VERSION_IS_PRERELEASE")
		versionType = Boolean.parseBoolean(isPreRelease) ? "beta" : "release"

		if (project.mod_version.contains("beta")) versionType = "beta"
		if (project.mod_version.contains("alpha")) versionType = "alpha"


		// Files
		uploadFile = remapJar.archiveFile
		additionalFiles = [sourcesJar.archiveFile]


		// Project info
		syncBodyFrom = rootProject.file("README.md").text
		def changelogEnv = System.getenv("VERSION_CHANGELOG")
		if (changelogEnv != null) changelog = changelogEnv


		// Dependencies
		dependencies {
            required.project "fabric-api"
		}
	}
}
