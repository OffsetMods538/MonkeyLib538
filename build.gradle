plugins {
	id 'java'
    id 'fabric-loom' version '1.11-SNAPSHOT' apply false
	id 'io.github.dexman545.outlet' version '1.6.1' apply false
	id 'com.modrinth.minotaur' version '2.+' apply false
}

ext {
	versionPrefix = rootProject.mod_version
	if ("true".equalsIgnoreCase(System.getenv("IS_DEBUG"))) {
		versionPrefix = "${versionPrefix}-${System.currentTimeMillis()}"
	}
	final String versionSuffix = System.getenv("VERSION_SUFFIX")
	if (versionSuffix != null && !versionSuffix.isEmpty()) {
		versionPrefix = "${versionPrefix}-${versionSuffix}"
	}

    System.out.println("Version Prefix: " + versionPrefix)
}

allprojects {
	group = rootProject.maven_group
}

configure(subprojects.findAll {it.name == "common" || it.name != it.project_type}) {
	apply plugin: "java"
    apply plugin: "maven-publish"

	base.archivesName = "monkeylib538-${project.project_type}"

	repositories {
		mavenCentral()
		maven {
			name = "OffsetMods538"
			url = "https://maven.offsetmonkey538.top/releases"
			content {
				includeGroup "top.offsetmonkey538.offsetconfig538"
			}
		}
	}

	dependencies {
		compileOnly "org.jetbrains:annotations:${rootProject.jetbrainsannotations_version}"
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = 21
	}

	java {
		withSourcesJar()
		withJavadocJar()

		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
	}

	tasks.named("javadoc", Javadoc) {
		options.addFileOption('-add-stylesheet', rootProject.file("javadoc-stylesheet.css"))

		options {
			links(
					"https://maven.offsetmonkey538.top/javadoc/releases/top/offsetmonkey538/offsetconfig538/offsetconfig538/${rootProject.offsetconfig538_version}/raw/"
			)
		}
	}

	jar {
		from("${rootProject.projectDir}/LICENSE") {
			rename { "${it}" }
		}
	}

	publishing {
		repositories {
			maven {
				name = "OffsetMonkey538"
				url = "https://maven.offsetmonkey538.top/releases"
				credentials {
					username = providers.gradleProperty("OffsetMonkey538Username").getOrElse(System.getenv("MAVEN_USERNAME"))
					password = providers.gradleProperty("OffsetMonkey538Password").getOrElse(System.getenv("MAVEN_PASSWORD"))
				}
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
		publications {
			maven(MavenPublication) {
				artifactId = base.archivesName.get()


				from components.java
			}
		}
	}
}

configure(subprojects.findAll {it.name != "common" && it.name != it.project_type}) {
	apply plugin: "com.modrinth.minotaur"

	version = "${rootProject.versionPrefix}"
	if (project.hasProperty("minecraft_version")) version = "${version}+${project.minecraft_version}"

	configurations {
		common {
			canBeResolved = true
			canBeConsumed = false
		}
		api.extendsFrom common
	}

	dependencies {
		common project(path: ":common")
	}

	tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
}
