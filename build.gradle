import net.fabricmc.loom.task.RemapJarTask
import dex.plugins.outlet.v2.util.ReleaseType

plugins {
	id 'java'
	id 'fabric-loom' version '1.10-SNAPSHOT' apply false
	id 'io.github.dexman545.outlet' version '1.6.1' apply true
	id 'com.modrinth.minotaur' version '2.+' apply false
}

outlet {
    maintainPropertiesFile = System.getenv("DISABLE_PROPERTIES_UPDATE") == null
    mcVersionRange = "*"
	propertiesData = [
            'loader_version': outlet.loaderVersion()
    ]
}

ext {
	debugVersion = ""
	if ("true".equalsIgnoreCase(System.getenv("IS_DEBUG"))) debugVersion = System.currentTimeMillis()

	final String customVersion = System.getenv("CUSTOM_VERSION")
	if (customVersion != null && !customVersion.isEmpty()) debugVersion = customVersion
}

allprojects {
	group = rootProject.maven_group
}

subprojects {
	apply plugin: "fabric-loom"
    apply plugin: "maven-publish"

    outlet {
        maintainPropertiesFile = System.getenv("DISABLE_PROPERTIES_UPDATE") == null
        mcVersionRange = project.supported_minecraft_versions
        allowedReleaseTypes = Set.of(ReleaseType.RELEASE)
        propertiesData = [
                'yarn_version': outlet.yarnVersion(project.minecraft_version),
                'fapi_version': outlet.fapiVersion(project.minecraft_version)
        ]
    }

	repositories {
		mavenCentral()
	}

	dependencies {
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
		modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"

		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fapi_version}"
	}

	processResources {
		inputs.properties(Map.of(
				"modVersion",                 rootProject.mod_version,
				"supportedMinecraftVersions", project.supported_minecraft_versions
		))

		filesMatching("fabric.mod.json") {
			expand(Map.of(
					"modVersion",                 rootProject.mod_version,
					"supportedMinecraftVersions", project.supported_minecraft_versions
			))
		}

		exclude ".cache/**"
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = 17
	}

	java {
		withSourcesJar()
		withJavadocJar()

		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17
	}

	tasks.named("javadoc", Javadoc) {
		options.addFileOption('-add-stylesheet', rootProject.file("javadoc-stylesheet.css"))
	}

	jar {
		from("${rootProject.projectDir}/LICENSE") {
			rename { "${it}" }
		}
	}

	publishing {
		repositories {
			maven {
				name = "OffsetMonkey538"
				url = "https://maven.offsetmonkey538.top/releases"
				credentials {
					username = providers.gradleProperty("OffsetMonkey538Username").getOrElse(System.getenv("MAVEN_USERNAME"))
					password = providers.gradleProperty("OffsetMonkey538Password").getOrElse(System.getenv("MAVEN_PASSWORD"))
				}
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
		publications {
			maven(MavenPublication) {
				if (project.name == "common") {
					artifactId = "common"
				} else {
					artifactId = "monkeylib538"
				}

				from(components["java"])
			}
		}
	}
	tasks.publishMavenPublicationToMavenLocal.doLast {
		if (System.getenv("IS_DEBUG") == "true") System.out.println("Version: " + version)
	}
	tasks.publishMavenPublicationToOffsetMonkey538Repository.doLast {
		if (System.getenv("IS_DEBUG") == "true") System.out.println("Version: " + version)
	}
}

configure(subprojects.findAll { it.name == "common" }) {
	version = rootProject.mod_version
	if (rootProject.debugVersion != "") version = "${version}+${rootProject.debugVersion}"

	base.archivesName = "common"

	configurations {
		includeApi

		include.extendsFrom includeApi
		api.extendsFrom     includeApi
	}

	dependencies {
		includeApi "blue.endless:jankson:${project.jankson_version}"
	}
}

configure(subprojects.findAll {it.name != "common"}) {
	apply plugin: "com.modrinth.minotaur"

	version = "${rootProject.mod_version}+${project.minecraft_version}"
	if (rootProject.debugVersion != "") version = "${version}-${rootProject.debugVersion}"

	base.archivesName = "monkeylib538"

	loom {
		splitEnvironmentSourceSets()

		sourceSets {
			testmod {
				compileClasspath += main.compileClasspath
				runtimeClasspath += main.runtimeClasspath

				compileClasspath += client.compileClasspath
				runtimeClasspath += client.runtimeClasspath
			}
		}

		mods {
			monkeylib538 {
				sourceSet sourceSets.main
				sourceSet sourceSets.client
			}

			testmod {
				sourceSet sourceSets.testmod
			}
		}

		accessWidenerPath = file("src/main/resources/monkeylib538.accesswidener")

		runs {
			client {
				name = "${project.name} Client"
			}

			server {
				name = "${project.name} Server"
				runDir "run/server"
			}

			testmodClient {
				client()
				name = "${project.name} Testmod Client"
				runDir "run/testmod"
				source sourceSets.testmod
			}
		}

		createRemapConfigurations(sourceSets.testmod)
	}

	// https://gist.github.com/maityyy/3dbcd558d58a6412c3a2a38c72706e8e
	afterEvaluate {
		loom.runs.configureEach {
			vmArg "-javaagent:${configurations.compileClasspath.find{ it.name.contains("sponge-mixin") }}"
		}
	}

	configurations {
		common {
			canBeResolved = true
			canBeConsumed = false
		}
		api.extendsFrom common
	}

	dependencies {
		common(project(path: ":common", configuration: "namedElements")) {
			exclude(group: "net.fabricmc.fabric-api")
		}
		include(project(":common")) {
			exclude(group: "net.fabricmc.fabric-api")
		}

		testmodImplementation sourceSets.main.output
		testmodImplementation sourceSets.client.output
	}

	tasks.register('testmodJar', Jar) {
		from sourceSets.testmod.output
		destinationDirectory = project.layout.buildDirectory.dir("devlibs")
		archiveClassifier = "testmod"
	}

	tasks.register('remapTestmodJar', RemapJarTask) {
		dependsOn testmodJar
		input = testmodJar.archiveFile
		archiveClassifier = "testmod"
		addNestedDependencies = false
	}

	build.dependsOn remapTestmodJar

	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = "monkeylib538"
		def customVersionName = System.getenv("VERSION_NAME")
		if (customVersionName != null) versionName = customVersionName
		versionNumber = "${project.version}"
		versionType = "beta"
		uploadFile = remapJar.archiveFile
		//additionalFiles = [sourcesJar.archiveFile, javadocJar.archiveFile]
		additionalFiles = [sourcesJar.archiveFile]
		gameVersions = outlet.mcVersions()
		syncBodyFrom = rootProject.file("README.md").text
		def changelogFile = rootProject.file("CHANGELOG.md")
		if (changelogFile.exists()) {
			changelog = changelogFile.text
		}

		dependencies {
		}
	}

	tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
}
