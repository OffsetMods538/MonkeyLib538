import org.codehaus.groovy.runtime.GStringImpl

import java.nio.file.Files

plugins {
	id 'java'
	id 'java-library'
	id 'net.neoforged.moddev'           version "${neoforged_moddev}"            apply false
    id 'fabric-loom'                    version "${fabric_loom}"                 apply false
	id 'io.papermc.paperweight.userdev' version "${papermc_paperweight_userdev}" apply false
	id 'xyz.jpenilla.run-paper'         version "${jpenilla_run_task}"           apply false
	id 'com.gradleup.shadow'            version "${gradleup_shadow}"             apply false
	id 'xyz.jpenilla.resource-factory'  version "${jpenilla_resource_factory}"   apply false
	id 'io.github.dexman545.outlet'     version "${dexman_outlet}"               apply false
	id 'com.modrinth.minotaur'          version "${modrinth_minotaur}"           apply false
    id 'hamburg.janove.elevator-music'  version "0.1"
}

//elevatorMusic {
//    if (Boolean.parseBoolean(System.getenv("DISABLE_MUSIC"))) return
//    waitMusic = file("${rootProject.projectDir}/veryImportant/music.wav")
//    successSound = file("${rootProject.projectDir}/veryImportant/done.wav")
//}

ext {
	versionPrefix = rootProject.mod_version
	if (!Boolean.parseBoolean(System.getenv("IS_RELEASE"))) {
		String preReleaseVersion = System.currentTimeMillis()

		if (Boolean.parseBoolean(System.getenv("PRESERVE_PRERELEASE_VERSION"))) {
			var preReleaseVersionFile = file("preReleaseVersion.txt").toPath()
			if (Files.exists(preReleaseVersionFile)) preReleaseVersion = Files.readString(preReleaseVersionFile)
			Files.writeString(preReleaseVersionFile, preReleaseVersion)
		}

		var separator = "-"
		if (versionPrefix.contains("-")) separator = "."
		versionPrefix = "${versionPrefix}${separator}${preReleaseVersion}" as GStringImpl
	}
	final String versionSuffix = System.getenv("VERSION_SUFFIX")
	versionPrefix = "${versionPrefix}${versionSuffix == null ? "+local" : versionSuffix.isEmpty() ? "" : "+${versionSuffix}"}" as GStringImpl

	System.out.println("Version Prefix: " + versionPrefix)
}

subprojects {
	apply plugin: "java-library"

	repositories {
		mavenCentral()
		mavenLocal()
		exclusiveContent {
			forRepository {
				maven {
					name = "OffsetMods538"
					url = "https://maven.offsetmonkey538.top/releases"
				}
			}
			filter {
				includeGroupAndSubgroups "top.offsetmonkey538"
			}
		}
	}

	dependencies {
		api "top.offsetmonkey538.offsetconfig538:offsetconfig538:${rootProject.offsetconfig538_version}"
		compileOnlyApi "org.jspecify:jspecify:${rootProject.jspecify_version}"
	}
}
