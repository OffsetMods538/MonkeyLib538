import org.codehaus.groovy.runtime.GStringImpl

import java.nio.file.Files

plugins {
	id 'java'
    id 'fabric-loom' version '1.14-SNAPSHOT' apply false
    id 'hamburg.janove.elevator-music' version "0.1"
	id 'name.remal.classes-relocation' version '2.1.6' apply false
}

//elevatorMusic {
//    if (Boolean.parseBoolean(System.getenv("DISABLE_MUSIC"))) return
//    waitMusic = file("${rootProject.projectDir}/veryImportant/music.wav")
//    successSound = file("${rootProject.projectDir}/veryImportant/done.wav")
//}

ext {
	versionPrefix = rootProject.mod_version
	if (!Boolean.parseBoolean(System.getenv("IS_RELEASE"))) {
        String preReleaseVersion = System.currentTimeMillis()

        if (Boolean.parseBoolean(System.getenv("PRESERVE_PRERELEASE_VERSION"))) {
            var preReleaseVersionFile = file("preReleaseVersion.txt").toPath()
            if (Files.exists(preReleaseVersionFile)) preReleaseVersion = Files.readString(preReleaseVersionFile)
            Files.writeString(preReleaseVersionFile, preReleaseVersion)
        }

        var separator = "-"
        if (versionPrefix.contains("-")) separator = "."
		versionPrefix = "${versionPrefix}${separator}${preReleaseVersion}" as GStringImpl
	}
	final String versionSuffix = System.getenv("VERSION_SUFFIX")
	if (versionSuffix != null && !versionSuffix.isEmpty()) {
		versionPrefix = "${versionPrefix}+${versionSuffix}" as GStringImpl
	}

    System.out.println("Version Prefix: " + versionPrefix)
}

allprojects {
	repositories {
		exclusiveContent {
			forRepository {
				maven {
					name = "OffsetMods538"
					url = "https://maven.offsetmonkey538.top/releases"
				}
			}
			filter {
				includeGroupAndSubgroups "top.offsetmonkey538"
			}
		}
	}
}
